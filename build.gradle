plugins {
    id "java"
    id "application"
}

import org.gradle.internal.os.OperatingSystem

group "tk.minersonline.HistorySurvival"
version "1.0-SNAPSHOT"

sourceCompatibility = 17

project.ext.lwjglVersion = "3.3.2"
project.ext.jomlVersion = "1.10.1"
project.ext.guacamoleVersion = "0.3.1"
project.ext.imguiVersion = "1.86.10"
project.ext.lwjglNatives = ""
project.ext.jbulletVersion = "20101010-1"
project.ext.vecmathVersion = "1.3.1"

switch (OperatingSystem.current()) {
	case OperatingSystem.LINUX:
		def osArch = System.getProperty("os.arch")
		project.ext.lwjglNatives = osArch.startsWith("arm") || osArch.startsWith("aarch64")
			? "natives-linux-${osArch.contains("64") || osArch.startsWith("armv8") ? "arm64" : "arm32"}"
			: "natives-linux"
		break
	case OperatingSystem.MAC_OS:
		project.ext.lwjglNatives = System.getProperty("os.arch").startsWith("aarch64") ? "natives-macos-arm64" : "natives-macos"
		break
	case OperatingSystem.WINDOWS:
		def osArch = System.getProperty("os.arch")
		project.ext.lwjglNatives = osArch.contains("64")
			? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
			: "natives-windows-x86"
		break
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "https://repo1.maven.org/maven2/" }
}

dependencies {
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-assimp"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-stb"
    implementation "org.lwjgl.lwjgl:lwjgl_util:2.9.3"
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-assimp::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
    implementation "org.joml:joml:$jomlVersion"

    implementation "com.github.crykn.guacamole:gdx-desktop:$guacamoleVersion"
    implementation "io.github.spair:imgui-java-app:$imguiVersion"
    implementation "cz.advel.jbullet:jbullet:$jbulletVersion"
    implementation "java3d:vecmath:$vecmathVersion"
}

mainClassName = 'uk.minersonline.HistorySurvival.Application'
